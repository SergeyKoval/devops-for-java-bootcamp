plugins {
	id 'org.springframework.boot' version '3.3.0'
	id 'java'
	id 'checkstyle'
	id 'com.github.spotbugs' version '6.0.25'
	id 'org.owasp.dependencycheck' version '8.2.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
//	implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
}

test {
	useJUnitPlatform()
}

checkstyle {
	toolVersion = "10.17.0"
}

checkstyleMain
	.exclude('com/example/springboot/Application.java')
	.exclude('com/example/springboot/AppController.java')

def classLoader = plugins['com.github.spotbugs'].class.classLoader
def SpotBugsEffort = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Effort' )
def SpotBugsConfidence = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Confidence' )
spotbugs {
	toolVersion = "4.8.6"
	effort = SpotBugsEffort.MAX
	reportLevel = SpotBugsConfidence.LOW
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
	reports {
		xml.required.set(false)  // For XML reports
		html.required.set(true) // For HTML reports
	}
}

tasks.check.dependsOn dependencyCheckAnalyze
dependencyCheck {
	failBuildOnCVSS = 10
	formats = ['XML', 'HTML']
}
